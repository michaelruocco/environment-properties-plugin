plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.16.0'
    id 'pl.allegro.tech.build.axion-release' version '1.10.3'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'com.diffplug.spotless' version '5.11.0'
}

group = 'com.github.michaelruocco'

scmVersion {
    versionCreator 'versionWithBranch'
    tag {
        prefix = 'environment-properties-plugin'
    }
}
project.version = scmVersion.version

repositories {
    mavenCentral()
}

dependencies {
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.yaml:snakeyaml:1.29'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.21.0'
    testImplementation('org.spockframework:spock-core:2.0-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

//java {
//    withJavadocJar()
//    withSourcesJar()
//}

pluginBundle {
    website = 'https://github.com/michaelruocco/environment-properties-plugin'
    vcsUrl = 'https://github.com/michaelruocco/environment-properties-plugin.git'
    description = 'Gradle plugin that enables generation of environment properties'
    tags = ['environment', 'properties']
    version = project.version

    plugins {
        environmentPropertiesPlugin {
            id = 'com.github.michaelruocco.environment-properties'
            displayName = 'environment-properties-plugin'
        }
    }
}

spotless {
    groovy {
        importOrder('groovy.util.logging', '')
        indentWithSpaces(4)
        endWithNewline()

        replace 'Not enough space after if', 'if(', 'if ('
        replaceRegex 'Too much space after if', 'if +\\(', 'if ('

        replace 'Not enough space after for', 'for(', 'for ('
        replaceRegex 'Too much space after for', 'for +\\(', 'for ('

        replace 'Not enough space after while', 'while(', 'while ('
        replaceRegex 'Too much space after while', 'while +\\(', 'while ('
    }
}